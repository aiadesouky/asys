DECLARE
   V_ITEM_ORG_ASSIGNMENT_TBL   EGO_ITEM_PUB.ITEM_ORG_ASSIGNMENT_TBL_TYPE;
   X_INVENTORY_ITEM_ID         NUMBER;
   X_ORGANIZATION_ID           NUMBER;
   X_RETURN_STATUS             VARCHAR2 (100);
   X_MSG_COUNT                 NUMBER;
   X_MSG_DATA                  VARCHAR2 (500);
   V_ITEM_ID                   NUMBER;
   V_ITEM_CODE                 VARCHAR2 (30);
   V_ITEM_DESCRIPTION          VARCHAR2 (300);
   V_UOM_CODE                  VARCHAR2 (50);
   V_SEC_UOM_CODE              VARCHAR2 (50);
   V_TEMPLATE_ID               NUMBER;
   V_ORGANIZATION_ID           NUMBER;
BEGIN
   MO_GLOBAL.SET_POLICY_CONTEXT ('S', 81);
   FND_GLOBAL.APPS_INITIALIZE (USER_ID        => 2887,
                               RESP_ID        => 23118,
                               RESP_APPL_ID   => 426);

   FOR Z IN (SELECT * FROM FRT_INTERFACE)
   LOOP
      BEGIN
         V_ITEM_CODE := Z.C02;
         V_ITEM_DESCRIPTION := Z.C03;
         V_TEMPLATE_ID := Z.C13;
         V_UOM_CODE := Z.C04;
         V_SEC_UOM_CODE := Z.C05;
         V_ORGANIZATION_ID := 82;

         EGO_ITEM_PUB.PROCESS_ITEM (
            P_API_VERSION                  => 1.0,
            P_INIT_MSG_LIST                => 'T',
            P_COMMIT                       => 'F',
            P_TRANSACTION_TYPE             => 'CREATE',
            P_ITEM_NUMBER                  => V_ITEM_CODE,
            P_SEGMENT1                     => V_ITEM_CODE,
            P_ORGANIZATION_ID              => V_ORGANIZATION_ID,
            P_DESCRIPTION                  => V_ITEM_DESCRIPTION,
            P_LONG_DESCRIPTION             => V_ITEM_DESCRIPTION,
            P_INVENTORY_ITEM_STATUS_CODE   => 'Active',
            P_PRIMARY_UOM_CODE             => V_UOM_CODE,
            P_SECONDARY_UOM_CODE           => V_SEC_UOM_CODE,
            P_APPLY_TEMPLATE               => 'ALL',
            P_TEMPLATE_ID                  => V_TEMPLATE_ID, --SELECT * FROM MTL_ITEM_TEMPLATES_VL
            X_INVENTORY_ITEM_ID            => X_INVENTORY_ITEM_ID,
            X_ORGANIZATION_ID              => X_ORGANIZATION_ID,
            X_RETURN_STATUS                => X_RETURN_STATUS,
            X_MSG_COUNT                    => X_MSG_COUNT,
            X_MSG_DATA                     => X_MSG_DATA);
         V_ITEM_ID := X_INVENTORY_ITEM_ID;

         IF X_RETURN_STATUS = 'S'
         THEN
            DBMS_OUTPUT.PUT_LINE (
               Z.C02 || ' ' || X_RETURN_STATUS || ' Done!');
         ELSE
            DBMS_OUTPUT.PUT_LINE (
               'CREATE ERR: ' || V_ITEM_CODE || ' ' || X_MSG_DATA);
         END IF;

         COMMIT;

         V_ORGANIZATION_ID := Z.C01;
         V_ITEM_ORG_ASSIGNMENT_TBL (1).INVENTORY_ITEM_ID := V_ITEM_ID;
         V_ITEM_ORG_ASSIGNMENT_TBL (1).ORGANIZATION_ID := V_ORGANIZATION_ID;

         EGO_ITEM_PUB.PROCESS_ITEM_ORG_ASSIGNMENTS (
            P_API_VERSION               => 1.0,
            P_INIT_MSG_LIST             => 'T',
            P_COMMIT                    => 'F',
            P_ITEM_ORG_ASSIGNMENT_TBL   => V_ITEM_ORG_ASSIGNMENT_TBL,
            X_RETURN_STATUS             => X_RETURN_STATUS,
            X_MSG_COUNT                 => X_MSG_COUNT);

         IF X_RETURN_STATUS = 'S'
         THEN
            DBMS_OUTPUT.PUT_LINE (
               Z.C02 || ' ' || X_RETURN_STATUS || ' Done!');
         ELSE
            DBMS_OUTPUT.PUT_LINE (
               'ORG AS ERR: ' || V_ITEM_CODE || ' ' || X_MSG_DATA);
         END IF;

         COMMIT;
      EXCEPTION
         WHEN OTHERS
         THEN
            DBMS_OUTPUT.PUT_LINE (V_ITEM_CODE || ' ' || SQLERRM);
      END;
   END LOOP;
END;